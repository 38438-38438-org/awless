# PEG for awless template syntax
package ast

type Peg Peg {
 *AST
}

Script   <- BlankLine* Statement+ BlankLine* WhiteSpacing EndOfFile
Statement <- WhiteSpacing (Expr / Declaration / Comment) WhiteSpacing EndOfLine*
Action <- 'none' / 'create' / 'delete' / 'start' / 'stop' / 'update' / 'attach' / 'check' / 'detach'
Entity <- 'none' / 
          'vpc' / 'subnet' / 'instance' / 'volume' / 'tag' / 'keypair' / 'securitygroup' / 'internetgateway' / 'routetable' / 'route' /
          'loadbalancer' / 'listener' / 'targetgroup' / 
          'database' /
          'user' / 'group' / 'role' / 'policy' / 'accesskey' /
          'bucket' / 'storageobject' / 
          'subscription' / 'topic' / 'queue' / 
          'zone' / 'record'
Declaration <- <Identifier> { p.addDeclarationIdentifier(text) }
               Equal
               Expr
Expr <- <Action> { p.addAction(text) }
        MustWhiteSpacing <Entity> { p.addEntity(text) }
        (MustWhiteSpacing Params)? { p.LineDone() }

Params <- Param+
Param <- <Identifier> { p.addParamKey(text) }
         Equal
         Value
         WhiteSpacing

Identifier <- [a-zA-Z0-9-_.]+

Value <- HoleValue {  p.addParamHoleValue(text) }
        / AliasValue {  p.addParamValue(text) }
        / RefValue {  p.addParamRefValue(text) }
        / <CidrValue> { p.addParamCidrValue(text) }
        / <IpValue> { p.addParamIpValue(text) }
        / <CSVValue> {p.addCsvValue(text)}
        / <IntRangeValue> { p.addParamValue(text) }
        / <IntValue> { p.addParamIntValue(text) }
        / <StringValue> { p.addParamValue(text) }


StringValue <- [a-zA-Z0-9-._:/]+

CSVValue <- (StringValue WhiteSpacing ',' WhiteSpacing)+ StringValue
CidrValue <- [0-9]+.[0-9]+.[0-9]+.[0-9]+'/'[0-9]+
IpValue <- [0-9]+.[0-9]+.[0-9]+.[0-9]+
IntValue <- [0-9]+
IntRangeValue <- [0-9]+'-'[0-9]+

RefValue <- '$'<Identifier>
AliasValue <- <'@'StringValue>
HoleValue <- '{'WhiteSpacing<Identifier>WhiteSpacing'}'

Comment <- '#'(!EndOfLine .)* / '//'(!EndOfLine .)* { p.LineDone() }

WhiteSpacing <- Whitespace*
MustWhiteSpacing <- Whitespace+
Equal <- WhiteSpacing '=' WhiteSpacing
BlankLine <- WhiteSpacing EndOfLine { p.LineDone() }
Whitespace   <- ' ' / '\t'
EndOfLine <- '\r\n' / '\n' / '\r'
EndOfFile <- !.
